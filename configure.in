AC_PREREQ(2.57)
AC_INIT([TiVoBridge], [1.0.1], [brian@smittyware.com])
AC_CONFIG_SRCDIR([src/tivobridge.c])
AC_CONFIG_HEADER([config.h])
AC_CONFIG_AUX_DIR([config])
AM_INIT_AUTOMAKE(foreign)

# Checks for programs.
AC_PROG_CC

# Checks for libraries.
LIB_SOCKET_NSL

# Checks for header files.
AC_HEADER_STDC
AC_HEADER_TIME
AC_CHECK_HEADERS([arpa/inet.h fcntl.h netinet/in.h string.h sys/socket.h])
AC_CHECK_HEADERS([unistd.h sys/select.h syslog.h])

m4_include([ac_compile_check_sizeof.m4])
m4_include([ac_create_stdint_h.m4])
AC_CREATE_STDINT_H([src/my_stdint.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
TYPE_SOCKLEN_T
AC_C_CONST

# Checks for library functions.
AC_FUNC_FORK
AC_TYPE_SIGNAL
AC_CHECK_FUNCS([strchr])
AC_CHECK_FUNC([strdup],
  [ AC_DEFINE_UNQUOTED([HAVE_STRDUP],[1],[Define if strdup function exists]) ],
  [ AC_LIBOBJ([strdup]) ])
AC_FUNC_SELECT_ARGTYPES

# Checks for optional features

AC_ARG_ENABLE([mdns],
    AC_HELP_STRING([--enable-mdns],[use Multicast DNS support (default: no)]),
    [enable_mdns=$enableval], [enable_mdns=no])
if test "$enable_mdns" != "no"; then
  AC_DEFINE([ENABLE_MDNS], [1],
      [Define to 1 to enable Multicast DNS support])
fi

AC_OUTPUT([Makefile src/Makefile doc/Makefile])
